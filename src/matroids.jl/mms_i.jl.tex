\begin{figure}
  \begin{jllisting}
"""
  function mms_i(V, i)

Finds the maximin share of agent i in the instance V.
"""
function mms_i(V::MatroidRank, i)
  M_i = V.Ms[i]; n = na(V)

  # An initial partition into independent subsets (subjectively so for i).
  (A, _) = matroid_partition_knuth73([M_i for _ in 1:n])

  # Setup matrix D st D[j,k] v_i(A_j) - v_i(A_k) ∀ j,k ∈ [n].
  D = zeros(Int8, n, n)
  for j in 1:n, k in 1:n
      # v_i(A_p) = |A_p| since all sets in A are independent wrt M_i.
      D[j,k] = length(A[j]) - length(A[k])
  end

  jk = argmax(D)
  while D[jk] > 1
      j,k = Tuple(jk)

      # By the augmentation property, ∃g ∈ A_j st A_k + g ∈ I_i.
      g = nothing
      for h ∈ setdiff(A[j], A[k]) 
          if is_indep(M_i, A[k] ∪ h)
              g = h; break
          end 
      end

      # Update A.
      setdiff!(A[j], g); union!(A[k], g)

      # Update D.
      for l in 1:n
          D[j, l] -= 1; D[l, j] += 1 # A_j is one smaller.
          D[k, l] += 1; D[l, k] -= 1 # A_k is one larger.
      end

      jk = argmax(D)
  end
  
  return minimum(length, A)
end
  \end{jllisting}
  \caption{Maximin share computation.}
  \label{code:mms_i}
\end{figure}